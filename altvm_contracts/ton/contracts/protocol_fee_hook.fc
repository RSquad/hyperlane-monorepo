#include "imports/stdlib.fc";
#include "imports/ton_msg.fc";
#include "imports/op_codes.fc";
#include "imports/events.fc";
#include "imports/errors.fc";
#include "imports/hook_types.fc";

const int FWD_FEES = 2700000;

global int g::protocol_fee;
global int g::max_protocol_fee;
global slice g::beneficiary;
global slice g::owner;

() store_globals() impure inline_ref {
    cell data = begin_cell()
        .store_uint(g::protocol_fee, 128)
        .store_uint(g::max_protocol_fee, 128)
        .store_slice(g::beneficiary)
        .store_slice(g::owner)
        .end_cell();
    set_data(data);
}

() load_globals() impure inline_ref {
    slice ds = get_data().begin_parse();
    g::protocol_fee = ds~load_uint(128);
    g::max_protocol_fee = ds~load_uint(128);
    g::beneficiary = ds~load_msg_addr();
    g::owner = ds~load_msg_addr();
}

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if ( in_msg_body.slice_empty?() ) { ;; accept simple transfer
        return ();
    }
    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);
    if ( flags & 1 ) { ;; ignore all bounced messages
        return ();
    }

    slice msg_sender = cs~load_msg_addr();
    int op = in_msg_body~load_uint(32);
    int query_id = in_msg_body~load_uint(64);

    load_globals();

    if(op == op::post_dispatch) {
        int gas_fee = gasfee(
            gasconsumed() + 
            10000,
            false
        );
        throw_if(
            error::msg_value_too_low,
            msg_value < g::protocol_fee + gas_fee + storagefee() + FWD_FEES
        );
        cell body = begin_cell()
            .store_uint(op::dispatch, 32)
            .store_uint(query_id, 64)
            .store_uint(op::dispatch::default_hook, 32)
            .store_slice(in_msg_body)
            .end_cell();

        builder dafault_hook_msg = build_message(
            msg_sender,
            msg_value,
            true,
            null(),
            body
        );

        raw_reserve(storagefee() + g::protocol_fee, reserve_mode::balance_plus_this_amount);

        send_raw_message(dafault_hook_msg.end_cell(), send_mode::carry_remaining_balance);

        return store_globals();
    }

    if (op == op::set_protocol_fee) {
        int protocol_fee = in_msg_body~load_uint(128);
        throw_if(
            error::exceeds_max_protocol_fee,
            protocol_fee > g::max_protocol_fee
        );

        g::protocol_fee = protocol_fee;
        return store_globals();
    }

    if (op == op::set_beneficiary) {
        throw_unless(
            error::unauthorized_sender,
            equal_slices(msg_sender, g::owner));
        g::beneficiary = in_msg_body~load_msg_addr();
        return store_globals();
    }

    if (op == op::collect_protocol_fee) {
        raw_reserve(storagefee(), reserve_mode::balance_plus_this_amount);

        send_change(
            g::beneficiary,
            op,
            query_id,
            send_mode::carry_remaining_balance
        );
    }

    if (op == op::transfer_ownership) {
        throw_unless(
            error::unauthorized_sender,
            equal_slices(msg_sender, g::owner));
        g::owner = in_msg_body~load_msg_addr();
        return store_globals();
    }

    throw(error::unknown_op);
}

(int, int, slice, int) get_hook_data() method_id {
    load_globals();
    return (g::max_protocol_fee, g::protocol_fee, g::beneficiary, hook_type::protocol_fee);
}

